{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "414c41ef",
   "metadata": {},
   "source": [
    "# Tutorials\n",
    "\n",
    "## Data from Copernicus Marine\n",
    "\n",
    "This examples shows how to download sea surface temperature from the\n",
    "[Mediterranean Sea High Resolution and Ultra High Resolution Sea Surface Temperature Analysis ](https://doi.org/10.48670/moi-00172) provided by\n",
    "[Copernicus Marine Service](https://marine.copernicus.eu/).\n",
    "Username and password are obtained after [registration](https://resources.marine.copernicus.eu/registration-form)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "85d556fc",
   "metadata": {},
   "source": [
    "The username and password can be added to the URL. For example\n",
    "`https://example.org/path` should become `https://username:password@example.org/path`:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be4eef97",
   "metadata": {},
   "outputs": [],
   "source": [
    "using NCDatasets, PyPlot, Statistics, URIs"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bcde8731",
   "metadata": {},
   "source": [
    "Add here your username and password so that the following lines read:\n",
    "```julia\n",
    "username = \"me\";\n",
    "password = \"paßwörth\";\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ff9b0c42",
   "metadata": {},
   "outputs": [],
   "source": [
    "username = ENV[\"CMEMS_USERNAME\"];\n",
    "password = ENV[\"CMEMS_PASSWORD\"];"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "50de83db",
   "metadata": {},
   "source": [
    "Add username and password to url"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da758838",
   "metadata": {},
   "outputs": [],
   "source": [
    "url = \"https://nrt.cmems-du.eu/thredds/dodsC/SST_MED_SST_L4_NRT_OBSERVATIONS_010_004_a_V2\"\n",
    "url2 = string(URI(URI(url),userinfo = string(username,\":\",password)))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3aff7742",
   "metadata": {},
   "source": [
    "Note this will output the URL with username/password. Add a semicolon to the\n",
    "next line to surpress the output"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e51cb4aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "ds = NCDataset(url2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a86422aa",
   "metadata": {},
   "source": [
    "Load the data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f698d41a",
   "metadata": {},
   "outputs": [],
   "source": [
    "ncvar = ds[\"analysed_sst\"];\n",
    "SST = ncvar[:,:,1]\n",
    "lon = ds[\"lon\"][:]\n",
    "lat = ds[\"lat\"][:]\n",
    "time = ds[\"time\"][1]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e7af58af",
   "metadata": {},
   "source": [
    "Make a plot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6a082b9f",
   "metadata": {},
   "outputs": [],
   "source": [
    "clf()\n",
    "pcolormesh(lon,lat,nomissing(SST,NaN)')\n",
    "cbar = colorbar(orientation=\"horizontal\")\n",
    "cbar.set_label(ncvar.attrib[\"units\"])\n",
    "gca().set_aspect(1/cosd(mean(lat)))\n",
    "title(\"$(ncvar.attrib[\"long_name\"]) $time\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "eaeda63c",
   "metadata": {},
   "source": [
    "Alternatively, one can also create the files `.netrc` and `.ncrc` in the your home directory with your credentials as explained for the NASA EarthData example which allows you to manage your passwords in a central configuration file and reduce the risk to accidentally share your credentials."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e32c3be4",
   "metadata": {},
   "source": [
    "## Data from NASA EarthData\n",
    "\n",
    "This example shows show to download data via OPeNDAP from the [NASA EarthData](https://www.earthdata.nasa.gov/)\n",
    "which requires a username and password.\n",
    "\n",
    "You need to be registered at [https://urs.earthdata.nasa.gov/users/new](https://urs.earthdata.nasa.gov/users/new)\n",
    "to get your credentials.\n",
    "\n",
    "Simply adding username and password to the URL for NASA Earth Data OPeNDPAP services is unfortunately not sufficient as it uses a different authentication mechanism.\n",
    "\n",
    "Create a `.netrc` file with the following content in your home directory:\n",
    "\n",
    "```\n",
    "machine urs.earthdata.nasa.gov\n",
    "    login YOUR_USERNAME\n",
    "    password YOUR_PASSWORD\n",
    "```\n",
    "\n",
    "where `YOUR_USERNAME` and `YOUR_PASSWORD` is your Earth Data username and password.\n",
    "\n",
    "Create a `.ncrc` file with the following content in your home directory:\n",
    "\n",
    "```\n",
    "HTTP.NETRC=/home/abarth/.netrc\n",
    "```\n",
    "\n",
    "where `HTTP.NETRC` is the full path to your new `.netrc` file.\n",
    "Note: if you are using docker, your home directory is `/home/jovan/`.\n",
    "You can test whether your configuration files are correct independently of NCDatasets\n",
    "by using the tool `ncdump` (one line):\n",
    "\n",
    "```bash\n",
    "ncdump -h \"https://opendap.earthdata.nasa.gov/providers/POCLOUD/collections/GHRSST%20Level%204%20MUR%20Global%20Foundation%20Sea%20Surface%20Temperature%20Analysis%20(v4.1)/granules/20190101090000-JPL-L4_GHRSST-SSTfnd-MUR-GLOB-v02.0-fv04.1\"\n",
    "```\n",
    "\n",
    "This should return the metadata of the OPeNDAP resource:\n",
    "\n",
    "```\n",
    "netcdf \\20190101090000-JPL-L4_GHRSST-SSTfnd-MUR-GLOB-v02.0-fv04 {\n",
    "dimensions:\n",
    "\tlat = 17999 ;\n",
    "\tlon = 36000 ;\n",
    "\ttime = 1 ;\n",
    "variables:\n",
    "\tshort analysed_sst(time, lat, lon) ;\n",
    "[...]\n",
    "```\n",
    "\n",
    "\n",
    "This is the typically error message which is returned when the credentials are not configured properly:\n",
    "\n",
    "```\n",
    "syntax error, unexpected WORD_WORD, expecting SCAN_ATTR or SCAN_DATASET or SCAN_ERROR\n",
    "context: HTTP^ Basic: Access denied.\n",
    "```\n",
    "\n",
    "When there is an error on the server side (500 internal server error), you might get the following error:\n",
    "\n",
    "```\n",
    "syntax error, unexpected $end, expecting SCAN_ATTR or SCAN_DATASET or SCAN_ERROR\n",
    "context: ^\n",
    "ncdump: https://opendap.earthdata.nasa.gov/providers/POCLOUD/collections/GHRSST%20Level%204%20MUR%20Global%20Foundation%20Sea%20Surface%20Temperature%20Analysis%20(v4.1)/granules/20190101090000-JPL-L4_GHRSST-SSTfnd-MUR-GLOB-v02.0-fv04.1: NetCDF: Malformed or inaccessible DAP2 DDS or DAP4 DMR response\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e035c1bd",
   "metadata": {},
   "source": [
    "Creating these configuration files is a bit fidely from jupyerlab.\n",
    "You can create these programmatically via (where you need to put your username and password):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "94303b3c",
   "metadata": {},
   "outputs": [],
   "source": [
    "write(expanduser(\"~/.netrc\"),\"\"\"\n",
    "machine urs.earthdata.nasa.gov\n",
    "    login USERNAME\n",
    "    password PASSWORD\n",
    "\"\"\");\n",
    "\n",
    "write(expanduser(\"~/.ncrc\"),\"\"\"\n",
    "HTTP.NETRC=$(expanduser(\"~/.netrc\"))\n",
    "\"\"\");"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "66a0d6e9",
   "metadata": {},
   "source": [
    "Check the content of the file with:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "32e62a5f",
   "metadata": {},
   "outputs": [],
   "source": [
    "print(read(expanduser(\"~/.netrc\"),String))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ce288741",
   "metadata": {},
   "outputs": [],
   "source": [
    "print(read(expanduser(\"~/.ncrc\"),String))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2c62a67f",
   "metadata": {},
   "source": [
    "> **_Important:_** You must *restart* julia (e.g. Kernel > Restart Kernel if you use Jupyterlab)\n",
    "\n",
    "Here we use the [GHRSST Level 4 MUR Global Foundation Sea Surface Temperature Analysis (v4.1)](https://podaac.jpl.nasa.gov/dataset/MUR-JPL-L4-GLOB-v4.1) dataset.\n",
    "In the following example, we download the data via OPeNDAP for a chosen bounding box and given time instance."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b09e2512",
   "metadata": {},
   "outputs": [],
   "source": [
    "using NCDatasets, PyPlot, Dates, Statistics\n",
    "\n",
    "url = \"https://opendap.earthdata.nasa.gov/providers/POCLOUD/collections/GHRSST%20Level%204%20MUR%20Global%20Foundation%20Sea%20Surface%20Temperature%20Analysis%20(v4.1)/granules/20190101090000-JPL-L4_GHRSST-SSTfnd-MUR-GLOB-v02.0-fv04.1\"\n",
    "\n",
    "ds = NCDataset(url)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9f55c880",
   "metadata": {
    "lines_to_next_cell": 2
   },
   "source": [
    "Did you got an \"Access denied\" error? If yes, please check:\n",
    "* check your credentials at https://urs.earthdata.nasa.gov/\n",
    "* check the content of your files in `~/.ncrc` and `~/.netrc`\n",
    "* restart Julia\n",
    "* check the `ncdump -h` shell command above in a terminal\n",
    "* be aware that a `.ncrc` file in your current directory overrides the file in your home directory"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bcaad7a2",
   "metadata": {},
   "source": [
    "Bounding box"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "896e92ad",
   "metadata": {},
   "outputs": [],
   "source": [
    "# range of longitude\n",
    "lonr = (-6, 37.0)\n",
    "# range of latitude\n",
    "latr = (29, 45.875)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "91c00a83",
   "metadata": {},
   "source": [
    "Load a subset"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "26e22fb4",
   "metadata": {},
   "outputs": [],
   "source": [
    "ds_subset = NCDatasets.@select(\n",
    "    ds[\"analysed_sst\"],\n",
    "    $lonr[1] <= lon <= $lonr[2] && $latr[1] <= lat <= $latr[2])\n",
    "\n",
    "ncvar = ds_subset[\"analysed_sst\"]\n",
    "SST = ncvar[:,:,1]\n",
    "lon = ds_subset[\"lon\"][:]\n",
    "lat = ds_subset[\"lat\"][:]\n",
    "time = ds_subset[\"time\"][1]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c7715b60",
   "metadata": {},
   "source": [
    "Make a plot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c254c01c",
   "metadata": {
    "lines_to_next_cell": 2
   },
   "outputs": [],
   "source": [
    "clf()\n",
    "pcolormesh(lon,lat,nomissing(SST,NaN)');\n",
    "gca().set_aspect(1/cosd(mean(lat)))\n",
    "cbar = colorbar(orientation=\"horizontal\")\n",
    "cbar.set_label(ncvar.attrib[\"units\"])\n",
    "plt.title(\"$(ncvar.attrib[\"long_name\"]) $time\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "22192817",
   "metadata": {},
   "source": [
    "## Access data from S3 object storage"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "849e409a",
   "metadata": {},
   "source": [
    "NASA EarthData is also available from AWS S3 object storage at the AWS region us-west-2. This example assumes that you have access to an AWS instance in this region (available thought e.g. [Pangeo Cloud](https://pangeo.io/cloud.html) after registration thanks to funding from the NSF and the fine folks at [Pangeo](https://github.com/pangeo-data)).\n",
    "\n",
    "The S3 access token available from [https://archive.podaac.earthdata.nasa.gov/s3credentials](https://archive.podaac.earthdata.nasa.gov/s3credentials) are only valid for 1 hour. It is therefore preferable to get this token programmatically using your EarthData username and password. The function `earthdata_s3credentials` is a translation of [\"Sample script to receive credentials\"](https://web.archive.org/web/20220623090241/https://nasa.github.io/cumulus-distribution-api/#temporary-s3-credentials) to Julia:\n",
    "\n",
    "```julia\n",
    "using AWS, Base64, JSON3, HTTP, NCDatasets, URIs\n",
    "using AWS: @service\n",
    "@service S3\n",
    "\n",
    "function earthdata_s3credentials(\n",
    "    username, password;\n",
    "    credentials_url = \"https://archive.podaac.earthdata.nasa.gov/s3credentials\")\n",
    "\n",
    "    origin = URI(credentials_url).scheme\n",
    "\n",
    "    resp = HTTP.get(credentials_url,redirect=false)\n",
    "    authorize_url = Dict(resp.headers)[\"Location\"]\n",
    "\n",
    "    auth = base64encode(string(username,\":\",password))\n",
    "\n",
    "    resp2 = HTTP.post(authorize_url,[\"Origin\" => origin],\n",
    "                      \"credentials=$auth\",redirect=false)\n",
    "    redirect_url=Dict(resp2.headers)[\"Location\"]\n",
    "\n",
    "    # cookie jar\n",
    "    jar = Dict{String, Set{HTTP.Cookie}}()\n",
    "\n",
    "    # raises a 500 error, as in the shell script, but we get a cookie!\n",
    "    HTTP.get(redirect_url; cookies=true,\n",
    "             cookiejar = jar, redirect = false,\n",
    "             status_exception = false)\n",
    "\n",
    "    resp4 = HTTP.get(credentials_url; cookies=true, cookiejar = jar, redirect = false)\n",
    "\n",
    "    cred = JSON3.read(resp4.body)\n",
    "    return cred\n",
    "end\n",
    "\n",
    "# add your credentials here (or get it programmatically from environment variables or a file)\n",
    "username = \"...\"\n",
    "password = \"...\"\n",
    "\n",
    "cred = earthdata_s3credentials(username,password)\n",
    "@info \"Token expires: $(cred.expiration)\"\n",
    "\n",
    "\n",
    "ENV[\"AWS_ACCESS_KEY_ID\"] = cred.accessKeyId;\n",
    "ENV[\"AWS_SECRET_ACCESS_KEY\"] = cred.secretAccessKey;\n",
    "ENV[\"AWS_SESSION_TOKEN\"] = cred.sessionToken;\n",
    "\n",
    "c = AWS.global_aws_config();\n",
    "\n",
    "resp = S3.list_objects(\"podaac-ops-cumulus-protected\",\n",
    "     Dict(\"prefix\" => \"MODIS_TERRA_L3_SST_MID-IR_DAILY_4KM_NIGHTTIME_V2019.0/\",\n",
    "          \"delimiter\" => '/'))\n",
    "\n",
    "# download the first object\n",
    "data = S3.get_object(\"podaac-ops-cumulus-protected\",resp[\"Contents\"][1][\"Key\"]);\n",
    "\n",
    "# load the NetCDF dataset\n",
    "ds = NCDataset(\"temp-memory\",\"r\",memory = data)\n",
    "```\n",
    "\n",
    "Output:\n",
    "```\n",
    "CDataset: temp-memory\n",
    "Group: /\n",
    "\n",
    "Dimensions\n",
    "   lat = 4320\n",
    "   lon = 8640\n",
    "   rgb = 3\n",
    "   eightbitcolor = 256\n",
    "\n",
    "Variables\n",
    "  sst4   (8640 × 4320)\n",
    "    Datatype:    Int16\n",
    "    Dimensions:  lon × lat\n",
    "    Attributes:\n",
    "     long_name            = 4um Sea Surface Temperature\n",
    "[...]\n",
    "```\n",
    "\n",
    "The example requires NCDatasets 0.12.5 which allows one to read a NetCDF dataset directly from a vector of bytes in memory.\n",
    "\n",
    "To debug, it is useful to run the `aws` shell command to list all keys in the buckets (it requires the `AWS_*` environment variables to be set):\n",
    "\n",
    "```bash\n",
    "aws s3 ls s3://podaac-ops-cumulus-protected/MODIS_TERRA_L3_SST_THERMAL_DAILY_4KM_NIGHTTIME_V2019.0/\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.2",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
