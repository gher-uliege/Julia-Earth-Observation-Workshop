{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "1ee49807",
   "metadata": {},
   "source": [
    "# Tutorial on DINCAE\n",
    "\n",
    "This script/notebook reconstructs missing data in satellite data using a neural\n",
    "network architecture called convolutional auto-encoder described in the\n",
    "following articles:\n",
    "\n",
    "* Barth, A., Alvera-Azcárate, A., Licer, M., and Beckers, J.-M.: DINCAE 1.0: a convolutional neural network with error estimates to reconstruct sea surface temperature satellite observations, Geosci. Model Dev., 13, 1609–1622, https://doi.org/10.5194/gmd-13-1609-2020, 2020.\n",
    "* Barth, A., Alvera-Azcárate, A., Troupin, C., & Beckers, J.-M. (2022). DINCAE 2.0: multivariate convolutional neural network with error estimates to reconstruct sea surface temperature satellite and altimetry observations. Geoscientific Model Development, 15(5), 2183–2196. https://doi.org/10.5194/gmd-15-2183-2022\n",
    "\n",
    "The example here uses MODIS sea surface temperature from the Physical\n",
    "Oceanography Distributed Active Archive Center (PO.DAAC) JPL, NASA.\n",
    "More information is available at https://dx.doi.org/10.5067/MODST-1D4D9\n",
    "\n",
    "This notebook/script is indented to be run on a GPU with CUDA support (NVIDIA GPU)\n",
    "with a least 8 GB of RAM."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a499e66a",
   "metadata": {},
   "source": [
    "Dependencies are installed with:\n",
    "\n",
    "```julia\n",
    "using Pkg\n",
    "Pkg.add([\"CUDA\",\"Knet\",\"NCDatasets\",\"PyPlot\"])\n",
    "Pkg.add(url=\"https://github.com/gher-uliege/DINCAE_utils.jl\", rev=\"main\")\n",
    "```\n",
    "\n",
    "See also https://github.com/gher-ulg/DINCAE.jl#installation\n",
    "\n",
    "Load the necessary modules"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a25050f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "using CUDA\n",
    "using DINCAE\n",
    "using DINCAE_utils\n",
    "using Dates\n",
    "using Knet\n",
    "using NCDatasets\n",
    "using PyPlot\n",
    "using Printf\n",
    "using Downloads: download"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dbf15809",
   "metadata": {
    "lines_to_next_cell": 2
   },
   "source": [
    "## Data download/processing\n",
    "\n",
    "First we define the domain and time interval.\n",
    "\n",
    "The example is tested here for a short time frame but for realistic\n",
    "applications one should use a much longer time-range (like 10, 20 years or more)\n",
    "\n",
    "Place the files modis_subset.nc and data-avg.nc (if you have them already)\n",
    "in your current working directory.\n",
    "They are downloaded is not present, but they are very large (400 MB)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fc16c475",
   "metadata": {},
   "outputs": [],
   "source": [
    "# longitude range (east, west)\n",
    "lon_range = [-33, -24]\n",
    "# latitude range (south, north)\n",
    "lat_range = [33.0, 40.0]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "354ca392",
   "metadata": {},
   "outputs": [],
   "source": [
    "# time range (start, end)\n",
    "time_range = [DateTime(2017,1,1), DateTime(2019,12,31)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d94e677c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# local directory\n",
    "localdir = \".\"\n",
    "# create directory\n",
    "mkpath(localdir)\n",
    "# filename of the subset\n",
    "fname_subset = joinpath(localdir,\"modis_subset.nc\")\n",
    "# filename of the clean data\n",
    "fname = joinpath(localdir,\"modis_cleanup.nc\")\n",
    "# filename of the data with added clouds for cross-validation\n",
    "fname_cv = joinpath(localdir,\"modis_cleanup_add_clouds.nc\")\n",
    "varname = \"sst\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "363aa40f",
   "metadata": {},
   "source": [
    "Results of DINCAE will be placed in a sub-directory under `localdir`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4038e15e",
   "metadata": {},
   "outputs": [],
   "source": [
    "outdir = \".\"\n",
    "mkpath(outdir)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "318230e5",
   "metadata": {},
   "source": [
    "Download the MODIS subset:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bd76e6fe",
   "metadata": {},
   "outputs": [],
   "source": [
    "if !isfile(fname_subset)\n",
    "    download(\"https://dox.ulg.ac.be/index.php/s/a1Lk2sG4onZR5Gg/download\",fname_subset)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cb6b0b91",
   "metadata": {},
   "source": [
    "The MODIS subset was generated by the following code:\n",
    "\n",
    "```julia\n",
    "for dt in time_range[1]:Day(1):time_range[end]\n",
    "    yyyy = Dates.format(dt,\"yyyy\")\n",
    "    mm = Dates.format(dt,\"mm\")\n",
    "    dd = Dates.format(dt,\"dd\")\n",
    "    doy = @sprintf(\"%03d\",Dates.dayofyear(dt))\n",
    "    url = \"https://podaac-opendap.jpl.nasa.gov:443/opendap/allData/modis/L3/terra/11um/v2019.0/4km/daily/$yyyy/$doy/TERRA_MODIS.$(yyyy)$(mm)$(dd).L3m.DAY.NSST.sst.4km.nc\"\n",
    "    filename = joinpath(localdir,\"TERRA_MODIS.$(yyyy)$(mm)$(dd).L3m.DAY.NSST.sst.4km.nc\")\n",
    "    if isfile(filename)\n",
    "        continue\n",
    "    end\n",
    "    try\n",
    "        ds = NCDataset(url)\n",
    "        ds_subset = NCDatasets.@select(ds,$(lon_range[1]) <= lon <= $(lon_range[2]) && $(lat_range[1]) <= lat <= $(lat_range[2]))\n",
    "        write(filename,ds_subset)\n",
    "        push!(filenames,filename)\n",
    "        close(ds)\n",
    "        println(\"$dt downloaded\")\n",
    "    catch e\n",
    "        showerror(stdout, e)\n",
    "        @warn(\"no data for $dt\")\n",
    "    end\n",
    "end\n",
    "\n",
    "filenames = String[]\n",
    "time = DateTime[]\n",
    "\n",
    "for dt in time_range[1]:Day(1):time_range[end]\n",
    "    yyyy = Dates.format(dt,\"yyyy\")\n",
    "    mm = Dates.format(dt,\"mm\")\n",
    "    dd = Dates.format(dt,\"dd\")\n",
    "    filename = joinpath(localdir,\"TERRA_MODIS.$(yyyy)$(mm)$(dd).L3m.DAY.NSST.sst.4km.nc\")\n",
    "    if isfile(filename)\n",
    "        push!(filenames,filename)\n",
    "        push!(time,dt)\n",
    "    end\n",
    "end\n",
    "\n",
    "ds = NCDataset(filenames,aggdim=\"time\",isnewdim=true,\n",
    "               constvars = [\"lon\",\"lat\"]);\n",
    "ds2 = NCDataset(fname_subset,\"c\")\n",
    "write(ds2,ds)\n",
    "defVar(ds2,\"time\",time,(\"time\",))\n",
    "close(ds2)\n",
    "close(ds)\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "457b4121",
   "metadata": {},
   "outputs": [],
   "source": [
    "ds = NCDataset(fname_subset,\"r\")\n",
    "sst = ds[\"sst\"][:,:,:];\n",
    "qual = ds[\"qual_sst\"][:,:,:];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3c873b8d",
   "metadata": {},
   "outputs": [],
   "source": [
    "@show extrema(skipmissing(qual))\n",
    "@show extrema(skipmissing(sst))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3aaf0758",
   "metadata": {},
   "source": [
    "We ignore all data points with missing quality flags,\n",
    "quality indicator exceeding 3 and temperature\n",
    "larger than 40°C (not necessary for this particular dataset)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fe2f27aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "sst_t = copy(sst)\n",
    "sst_t[(qual .> 3) .& .!ismissing.(qual)] .= missing\n",
    "sst_t[(sst_t .> 40) .& .!ismissing.(sst_t)] .= missing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "33cfa757",
   "metadata": {},
   "outputs": [],
   "source": [
    "@info \"number of missing observations: $(count(ismissing,sst_t))\"\n",
    "@info \"number of valid observations: $(count(.!ismissing,sst_t))\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c05a5a9d",
   "metadata": {},
   "source": [
    "Clean-up the data to them write to disk"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "13a17b86",
   "metadata": {},
   "outputs": [],
   "source": [
    "isfile(\"modis_cleanup.nc\") && rm(\"modis_cleanup.nc\")\n",
    "varname = \"sst\"\n",
    "fname = joinpath(localdir,\"modis_cleanup.nc\")\n",
    "ds2 = NCDataset(fname,\"c\")\n",
    "write(ds2,ds,exclude = [\"sst\",\"qual\"])\n",
    "defVar(ds2,varname,sst_t,(\"lon\",\"lat\",\"time\"))\n",
    "close(ds2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b88686ca",
   "metadata": {},
   "source": [
    "Add a land-sea mask to the file grid points with less than 5% of\n",
    "valid data are considerd as land"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d310b676",
   "metadata": {},
   "outputs": [],
   "source": [
    "DINCAE_utils.add_mask(fname,varname; minseafrac = 0.05)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "32a9a007",
   "metadata": {},
   "source": [
    "Choose cross-validation points by adding clouds to the cleanest\n",
    "images (copied from the cloudiest images). This function will generate\n",
    "a file `fname_cv`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a97d6fa7",
   "metadata": {},
   "outputs": [],
   "source": [
    "DINCAE_utils.addcvpoint(fname,varname; mincvfrac = 0.10);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "27ac717a",
   "metadata": {},
   "source": [
    "# Reconstruct missing data\n",
    "\n",
    "\n",
    "F is the floating point number type for neural network, here we use\n",
    "single precision"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "de2ba959",
   "metadata": {},
   "outputs": [],
   "source": [
    "const F = Float32"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "272af29e",
   "metadata": {},
   "source": [
    "Test if CUDA is functional to use the GPU, otherwise the CPU is used."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bf5bf72e",
   "metadata": {},
   "outputs": [],
   "source": [
    "if CUDA.functional()\n",
    "    Atype = KnetArray{F}\n",
    "else\n",
    "    @warn \"No supported GPU found. We will use the CPU which is very slow. Please check https://developer.nvidia.com/cuda-gpus\"\n",
    "    Atype = Array{F}\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7654324f",
   "metadata": {},
   "outputs": [],
   "source": [
    "Knet.atype() = Atype"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b1489806",
   "metadata": {},
   "source": [
    "Setting the paramenters of neural network\n",
    "see document of DINCAE.reconstruct for more information"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "240d8e68",
   "metadata": {
    "lines_to_next_cell": 2
   },
   "outputs": [],
   "source": [
    "epochs = 1000\n",
    "batch_size = 32\n",
    "enc_nfilter_internal = round.(Int,32 * 2 .^ (0:4))\n",
    "clip_grad = 5.0\n",
    "regularization_L2_beta = 0\n",
    "ntime_win = 3\n",
    "upsampling_method = :nearest\n",
    "loss_weights_refine = (0.3,0.7)\n",
    "save_epochs = 200:10:epochs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "752e5874",
   "metadata": {},
   "outputs": [],
   "source": [
    "data = [\n",
    "   (filename = fname_cv,\n",
    "    varname = varname,\n",
    "    obs_err_std = 1,\n",
    "    jitter_std = 0.05,\n",
    "    isoutput = true,\n",
    "   )\n",
    "]\n",
    "data_test = data;\n",
    "fnames_rec = [joinpath(outdir,\"data-avg.nc\")]\n",
    "data_all = [data,data_test]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "289759e9",
   "metadata": {},
   "source": [
    "Use these parameters for a quick test:"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "39dd4b7f",
   "metadata": {},
   "source": [
    "epochs = 10\n",
    "save_epochs = epochs:epochs"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "63a10a8e",
   "metadata": {},
   "source": [
    "Start the training and reconstruction of the neural network\n",
    "This takes a long time. You can cancel this step and get the\n",
    "sample result.\n",
    "\n",
    "```julia\n",
    "loss = DINCAE.reconstruct(\n",
    "    Atype,data_all,fnames_rec;\n",
    "    epochs = epochs,\n",
    "    batch_size = batch_size,\n",
    "    enc_nfilter_internal = enc_nfilter_internal,\n",
    "    clip_grad = clip_grad,\n",
    "    save_epochs = save_epochs,\n",
    "    upsampling_method = upsampling_method,\n",
    "    loss_weights_refine = loss_weights_refine,\n",
    "    ntime_win = ntime_win,\n",
    ")\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "506a2675",
   "metadata": {},
   "source": [
    "Download the result and load the loss"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "77f6ff16",
   "metadata": {
    "lines_to_next_cell": 2
   },
   "outputs": [],
   "source": [
    "if !isfile(fnames_rec[1])\n",
    "    download(\"https://dox.ulg.ac.be/index.php/s/91OnKoK0CUE3a8V/download\",fnames_rec[1])\n",
    "end\n",
    "loss =\n",
    "    NCDataset(fnames_rec[1]) do ds\n",
    "        ds[\"losses\"][:]\n",
    "    end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f1770602",
   "metadata": {},
   "source": [
    "Plot the loss function"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "df54ee47",
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(loss)\n",
    "ylim(extrema(loss[2:end]))\n",
    "xlabel(\"epochs\")\n",
    "ylabel(\"loss\");"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1f367ef3",
   "metadata": {},
   "source": [
    "# Post process results\n",
    "\n",
    "Compute the RMS with the independent validation data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d2383ac8",
   "metadata": {},
   "outputs": [],
   "source": [
    "case = (\n",
    "    fname_orig = fname,\n",
    "    fname_cv = fname_cv,\n",
    "    varname = varname,\n",
    ")\n",
    "fnameavg = joinpath(outdir,\"data-avg.nc\")\n",
    "cvrms = DINCAE_utils.cvrms(case,fnameavg)\n",
    "@info \"Cross-validation RMS error is: $cvrms\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9fc76b84",
   "metadata": {},
   "source": [
    "Next we plot all time instances. The figures with be placed in the\n",
    "directory `figdir`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "60c1cfea",
   "metadata": {},
   "outputs": [],
   "source": [
    "figdir = joinpath(outdir,\"Fig\")\n",
    "DINCAE_utils.plotres(case,fnameavg, clim = nothing, figdir = figdir,\n",
    "                     clim_quantile = (0.01,0.99),\n",
    "                     which_plot = :cv)\n",
    "@info \"Figures are in $(figdir)\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f58930ea",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "96592d7e",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.2",
   "language": "julia",
   "name": "julia-1.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
